#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#include "common_types.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba16) uniform image2D image;

layout(location = 0) rayPayloadEXT vec3 hit_value;

vec3 get_direction(in vec2 center, in Eye eye, in bool is_right)
{
    vec2 pixel_uv = center / vec2(gl_LaunchSizeEXT.xy);
    pixel_uv.x = pixel_uv.x * 2;
    pixel_uv.x = is_right ? pixel_uv.x - 1.0 : pixel_uv.x;
    return vec3(tan(eye.fov.left) + (pixel_uv.x) * (tan(eye.fov.right) - tan(eye.fov.left)),
        tan(eye.fov.up) + (pixel_uv.y) * (tan(eye.fov.down) - tan(eye.fov.up)),
        -1.0);
}

void shoot_ray(in vec3 direction, in Eye eye)
{
    direction = direction + 2.0 * cross(eye.pose.rotation.xyz, cross(eye.pose.rotation.xyz, direction) + eye.pose.rotation.w * direction);
    direction = normalize(direction);

    float tmin = 0.2;
    float tmax = 120.0;

    hit_value = vec3(0.0, 0.2, 0.0);
    traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, eye.pose.position, tmin, direction.xyz, tmax, 0);
}

vec3 shoot_ray(in vec2 center)
{
    vec3 direction = get_direction(center, scene_global.left, false);
    shoot_ray(direction, scene_global.left);
    return hit_value;
}

void main()
{
    vec3 color = 0.25 * shoot_ray(vec2(gl_LaunchIDEXT.xy) + vec2(0.25, 0.25));
    color += 0.25 * shoot_ray(vec2(gl_LaunchIDEXT.xy) + vec2(0.25, 0.75));
    color += 0.25 * shoot_ray(vec2(gl_LaunchIDEXT.xy) + vec2(0.75, 0.25));
    color += 0.25 * shoot_ray(vec2(gl_LaunchIDEXT.xy) + vec2(0.75, 0.75));
    imageStore(image, nonuniformEXT(ivec2(gl_LaunchIDEXT.xy)), vec4(color, 1.0));
}



